defmodule CommonUI.Components.Flash do
  @moduledoc false
  use CommonUI, :component

  import CommonUI.Components.Icon
  import CommonUI.Gettext, warn: false

  @doc """
  Renders flash notices.

  ## Examples

      <.flash kind={:info} flash={@flash} />
      <.flash kind={:info} phx-mounted={show("#flash")}>Welcome Back!</.flash>
  """
  attr :id, :string, default: "flash", doc: "the optional id of flash container"
  attr :flash, :map, default: %{}, doc: "the map of flash messages to display"
  attr :variant, :string, default: "fixed", values: ["fixed", "inline"]
  attr :title, :string, default: nil
  attr :kind, :atom, values: [:info, :warning, :error], doc: "used for styling and flash lookup"
  attr :autoshow, :boolean, default: true, doc: "whether to auto show the flash on mount"
  attr :close, :boolean, default: true, doc: "whether the flash can be closed"
  attr :class, :any, default: nil
  attr :rest, :global, doc: "the arbitrary HTML attributes to add to the flash container"

  slot :inner_block, doc: "the optional inner block that renders the flash message"

  def flash(%{variant: "inline"} = assigns) do
    ~H"""
    <div
      :if={msg = render_slot(@inner_block) || Phoenix.Flash.get(@flash, @kind)}
      id={@id}
      role="alert"
      class={[
        "rounded-lg p-3",
        @kind == :info && "bg-emerald-50 text-emerald-800 ring-emerald-500 fill-cyan-900",
        @kind == :warning && "bg-amber-50 text-amber-800 ring-amber-500 fill-amber-900",
        @kind == :error && "bg-rose-50 p-3 text-rose-900 ring-rose-500 fill-rose-900",
        @class
      ]}
      {@rest}
    >
      <p class="flex items-center gap-1.5 text-sm font-semibold">
        <.icon :if={@kind == :info} name={:information_circle} mini class="size-4" />
        <.icon :if={@kind == :warning} name={:exclamation_triangle} mini class="size-4" />
        <.icon :if={@kind == :error} name={:exclamation_circle} mini class="size-4" />
        <%= msg %>
      </p>
    </div>
    """
  end

  def flash(assigns) do
    ~H"""
    <div
      :if={msg = render_slot(@inner_block) || Phoenix.Flash.get(@flash, @kind)}
      id={@id}
      phx-mounted={@autoshow && show_flash("##{@id}")}
      phx-click={JS.push("lv:clear-flash", value: %{key: @kind}) |> hide_flash("#flash")}
      role="alert"
      class={[
        "fixed hidden bottom-4 right-4 w-80 sm:w-96 z-50 rounded-lg p-3 shadow-md shadow-gray-darkest/5 ring-1",
        @kind == :info && "bg-emerald-50 text-emerald-800 ring-emerald-500 fill-cyan-900",
        @kind == :warning && "bg-amber-50 text-amber-800 ring-amber-500 fill-amber-900",
        @kind == :error && "bg-rose-50 p-3 text-rose-900 ring-rose-500 fill-rose-900"
      ]}
      {@rest}
    >
      <p :if={@title} class="flex items-center gap-1.5 text-[0.8125rem] font-semibold leading-6">
        <.icon :if={@kind == :info} name={:information_circle} mini class="size-4" />
        <.icon :if={@kind == :warning} name={:exclamation_triangle} mini class="size-4" />
        <.icon :if={@kind == :error} name={:exclamation_circle} mini class="size-4" />
        <%= @title %>
      </p>
      <p class="mt-2 text-[0.8125rem] leading-5"><%= msg %></p>
      <button
        :if={@close}
        type="button"
        class="group absolute top-2 right-1 p-2"
        aria-label={gettext("close")}
      >
        <.icon name={:x_mark} class="h-5 w-5 stroke-current opacity-40 group-hover:opacity-70" />
      </button>
    </div>
    """
  end

  def show_flash(js \\ %JS{}, selector) do
    JS.show(js,
      to: selector,
      transition:
        {"transition-all transform ease-out duration-300", "opacity-0 translate-y-4 sm:translate-y-0 sm:scale-95",
         "opacity-100 translate-y-0 sm:scale-100"}
    )
  end

  def hide_flash(js \\ %JS{}, selector) do
    JS.hide(js,
      to: selector,
      time: 200,
      transition:
        {"transition-all transform ease-in duration-200", "opacity-100 translate-y-0 sm:scale-100",
         "opacity-0 translate-y-4 sm:translate-y-0 sm:scale-95"}
    )
  end
end
